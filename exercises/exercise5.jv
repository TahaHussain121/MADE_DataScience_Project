pipeline StopsPipeline {
    GTFSExtractor
    -> GTFSZipInterpreter
    -> GTFSStopsPicker;
    GTFSStopsPicker
    -> GTFSTextInterpreter
    -> GTFSCSVInterpreter
    -> StopsTableInterpreter
    -> StopsLoader;


    block GTFSExtractor oftype HttpExtractor {
        url: "https://gtfs.rhoenenergie-bus.de/GTFS.zip";
    }

    block GTFSZipInterpreter oftype ArchiveInterpreter {
        archiveType: "zip";
    }

    block GTFSStopsPicker oftype FilePicker {
        path: "./stops.txt";
    }

    block GTFSTextInterpreter oftype TextFileInterpreter { 
        encoding: "utf8";
    }

    block GTFSCSVInterpreter oftype CSVInterpreter {
        delimiter: ",";
        enclosing: '"';
    }

    block StopsTableInterpreter oftype TableInterpreter {
        header: true;
        columns: [
            "stop_id" oftype integer,
            "stop_name" oftype text,
            "stop_lat" oftype geo,
            "stop_lon" oftype geo,
            "zone_id" oftype zone
        ];
    }

    block StopsLoader oftype SQLiteLoader {
        table: "stops";
        file: "./gtfs.sqlite";
    }
}

valuetype zone oftype integer{
    constraints: [zone_id_1925];
}

constraint zone_id_1925 on integer:
    value == 1925;

valuetype geo oftype decimal{
    constraints: [geo_constraint];
}

constraint geo_constraint oftype RangeConstraint {
        lowerBound: -90;
        upperBound: 90;
        lowerBoundInclusive: true;
        upperBoundInclusive: true;
    }
